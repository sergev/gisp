#!/usr/bin/env gisp

func isNumberValue(expr) {
    return numberp(expr)
}

func isVariable(expr) {
    return symbolp(expr)
}

func sameVariable(v1, v2) {
    return eq(v1, v2)
}

func isPair(expr) {
    return pairp(expr)
}

func isTagged(expr, tag) {
    return isPair(expr) && eq(car(expr), tag)
}

func makeSum(a, b) {
    if isNumberValue(a) && a == 0 {
        return b
    }
    if isNumberValue(b) && b == 0 {
        return a
    }
    return cons(`'+, [a, b])
}

func makeProduct(a, b) {
    if (isNumberValue(a) && a == 0) || (isNumberValue(b) && b == 0) {
        return 0
    }
    if isNumberValue(a) && a == 1 {
        return b
    }
    if isNumberValue(b) && b == 1 {
        return a
    }
    return cons(`'*, [a, b])
}

func makeExponent(base, exponent) {
    return cons(`'pow, [base, exponent])
}

func sumAddend(expr) {
    return car(cdr(expr))
}

func sumAugend(expr) {
    return car(cdr(cdr(expr)))
}

func productMultiplier(expr) {
    return car(cdr(expr))
}

func productMultiplicand(expr) {
    return car(cdr(cdr(expr)))
}

func exponentBase(expr) {
    return car(cdr(expr))
}

func exponentPower(expr) {
    return car(cdr(cdr(expr)))
}

func deriv(expr, variable) {
    if isNumberValue(expr) {
        return 0
    }
    if isVariable(expr) {
        if sameVariable(expr, variable) {
            return 1
        }
        return 0
    }
    if isTagged(expr, `'+) {
        return makeSum(
            deriv(sumAddend(expr), variable),
            deriv(sumAugend(expr), variable)
        )
    }
    if isTagged(expr, `'*) {
        return makeSum(
            makeProduct(
                deriv(productMultiplier(expr), variable),
                productMultiplicand(expr)
            ),
            makeProduct(
                productMultiplier(expr),
                deriv(productMultiplicand(expr), variable)
            )
        )
    }
    if isTagged(expr, `'pow) {
        var base = exponentBase(expr)
        var power = exponentPower(expr)
        return makeProduct(
            makeProduct(power, deriv(base, variable)),
            makeExponent(base, makeSum(power, -1))
        )
    }
    return cons(`'unknown-derivative, [expr])
}

var expression = cons(`'*, [
    makeSum(`'x, 1),
    makeExponent(`'x, 3),
])

display(deriv(expression, `'x))
newline()
